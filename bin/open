#!/usr/bin/env zsh
#
# The same as double-clicking on file or dir $1 in X File Manager or Windows Explorer.

emulate zsh -o err_exit -o no_unset

if [[ $# != 1 || -z $1 ]]; then
  print -ru2 -- "usage: xopen FILE"
  exit 1
fi

case $OSTYPE in
  darwin*)
    # TODO
    exit 1
  ;;
  linux*)
    if [[ -r /proc/version && "$(</proc/version)" == *[Mm]icrosoft* ]]; then
      local ufile=${1:A}
      local wfile
      wfile=$(wslpath -wa -- $ufile)
      if [[ $wfile == '\\wsl$\'* || ! -d ${ufile:h} ]]; then
        builtin cd -q /mnt/c
        /mnt/c/Windows/System32/cmd.exe /c start $wfile
      else
        builtin cd -q -- ${ufile:h}
        /mnt/c/Windows/System32/cmd.exe /c start '.\'${ufile:t}
      fi
      exit
    elif (( $+commands[xdg-open] )); then
      command xdg-open $1 </dev/null &>/dev/null
      exit
    fi
  ;;
esac

print -ru2 -- "open: unsupported platform"
exit 1
