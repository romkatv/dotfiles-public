#!/usr/bin/env zsh
#
# Usage: open [OPTIONS] [--] FILE
#
# The same as double-clicking on the file in Finder, X File Manager or
# Windows Explorer. Options are platform-specific.

emulate zsh -o err_exit -o no_unset || exit

case $OSTYPE in
  darwin*)
    if [[ -e /usr/bin/open ]]; then
      exec -- /usr/bin/open "$@"
    fi
  ;|
  linux*)
    if [[ -r /proc/version && "$(</proc/version)" == *[Mm]icrosoft* &&
          -v commands[wslpath] && -v commands[powershell.exe] ]]; then
      if [[ ${1-} == -* ]]; then
        if [[ $1 == -- ]]; then
          shift
        else
          print -ru2 -- 'open: unsupported option: '${(q-)1}
          exit 1
        fi
      fi
      if [[ $# != 1 || -z $1 ]]; then
        print -ru2 -- 'usage: open [OPTIONS] [--] FILE'
        exit 1
      fi
      local wfile
      wfile=$(command wslpath -ma -- $1)
      exec -- powershell.exe -c "Invoke-Item -LiteralPath '${wfile//\'/''}'"
    elif [[ -v commands[xdg-open] ]]; then
      exec xdg-open "$@" </dev/null &>/dev/null
    fi
  ;|
  *)
    print -ru2 -- 'open: unsupported platform'
    exit 1
  ;;
esac
